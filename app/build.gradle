apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

ext {
    config = [
            versionMajor     : 0,
            versionMinor     : 1,
            versionPatch     : 0,
            versionClassifier: "",
            minimumSdkVersion: 15,
    ]
    buildVersionCode = {
        return config.minimumSdkVersion * 10000000 + config.versionMajor * 1000 +
                config.versionMinor * 100 + config.versionPatch
    }
    buildVersionName = {
        String versionName = "${config.versionMajor}.${config.versionMinor}.${config.versionPatch}"
        if (config.versionClassifier != null && !config.versionClassifier.isEmpty()) {
            versionName = versionName + "-" + config.versionClassifier
        }
        return versionName
    }
    packver = [
            supportLibrary: '26.1.0',
            retrofit      : '2.4.0',
            rxjava       : '2.2.2',
            rxandroid     : '2.1.0',
            picasso       : '2.71828',
    ]

}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.jquery404.foodie"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode buildVersionCode()
        versionName buildVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            versionNameSuffix ".debug"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_version", "${defaultConfig.versionName}"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // support library
    implementation "com.android.support:appcompat-v7:${packver.supportLibrary}"
    implementation "com.android.support:design:${packver.supportLibrary}"
    implementation "com.android.support:cardview-v7:${packver.supportLibrary}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // unit test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // image
    implementation "com.squareup.picasso:picasso:${packver.picasso}"

    // networking
    implementation "com.squareup.retrofit2:retrofit:${packver.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${packver.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${packver.retrofit}"
    implementation "io.reactivex.rxjava2:rxandroid:${packver.rxandroid}"
    implementation "io.reactivex.rxjava2:rxjava:${packver.rxjava}"

}
